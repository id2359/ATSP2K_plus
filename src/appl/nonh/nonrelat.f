*
*     ------------------------------------------------------------------
*      N O N R E L A T 
*     ------------------------------------------------------------------
*
*     THE ROUTINE EVALUATES THE NON-RELATIVISTIC HAMILTONIAN
*     WITH ORTHOGONAL ORBITALS
*
*
*     THE MATRIX ELEMENT OF THE TWO-ELECTRON POTENTIAL BETWEEN TWO
*     STATES (LABELLED 1 AND 2) MAY BE EXPRESSED AS A SUM OF WEIGHTED
*     RK (SLATER) INTEGRALS.  THIS SUBROUTINE, TOGETHER WITH THOSE
*     CALLED BY IT, DETERMINES THESE WEIGHTS, WHICH ARISE FROM AN
*     INTEGRATION OVER THE ANGULAR AND SPIN CO-ORDINATES
*     FOR DETAILS, SEE   U. FANO, PHYS. REV.,140,A67,(1965)
*
*     THE =INTERACTING= SHELLS ARE DESIGNATED  IRHO,ISIG,IRHOP,ISIGP.
*     THE FIRST TWO REFER TO THE L.H.S. OF     (PSI/V/PSIP)     , WHILE
*     THE SECOND TWO REFER TO THE R.H.S.  FOR DIAGONAL AND CERTAIN OFF-
*     DIAGONAL MATRIX ELEMENTS, THESE MAY NOT BE UNIQUE, AND EACH
*     POSSIBILITY MUST BE CONSIDERED IN TURN
*     THE CONDITION =IRHO .LE. ISIG ,  IRHOP .LE. ISIGP=  IS TO BE
*     SATISFIED
*
*     Written by G. Gaigalas,                                      * 
*     Vanderbilt University,  Nashville            February 1994   * 
*
      SUBROUTINE NONRELAT
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      COMMON/INFORM/IREAD,IWRITE,IOUT,ISC(8),ISCW
      COMMON/DEBUG/IBUG1,IBUG2,IBUG3,NBUG6,NBUG7,IFULL
      COMMON/MEDEFN/IHSH,NJ(16),LJ(16),NOSH1(16),NOSH2(16),J1QN1(31,3),
     :     J1QN2(31,3),IJFUL(16)
      COMMON/DIAGNL/IDIAG,JA,JB
    5 FORMAT(//10X,7H IRHO =,I3,4X,7H ISIG =,I3,4X,8H IRHOP =,I3,3X,8H I
     :SIGP =,I3)
      IZERO = 0
      IX=0
      IRHO=0
      ISIG=0
      IRHOP=0
      ISIGP=0
      IGGG=0
      DO 1 J=1,IHSH
        N=NOSH1(J)-NOSH2(J)
        IF(IABS(N).GT.2) RETURN
        IF(N.GT.0) THEN
          IF(N.EQ.1) THEN
            ISIG=J
            IF(IRHO.EQ.0) IRHO = J
            IX =IX+1
          ELSE
            IRHO=J
            IF(IABS(N).EQ.2)IGGG=20+IGGG
            IX=IX+2
          ENDIF
        ELSEIF(N.LT.0) THEN
          IF(N+1.EQ.0) THEN
            ISIGP=J
            IF(IRHOP.EQ.0) IRHOP = J
            IX=IX+1
          ELSE
            IRHOP=J
            IF(IABS(N).EQ.2)IGGG=20+IGGG
            IX=IX+2
          ENDIF
        ENDIF
    1 CONTINUE
*
*     IX MEASURES THE TOTAL NUMBER OF ELECTRONS IN EITHER CONFIGURATION
*     WHICH DO NOT OCCUR IN THE OTHER.  THEN IF  IX  IS GREATER THAN 4,
*     ORTHOGONALITY OF THE ORBITALS PREVENTS A NON-ZERO MATRIX ELEMENT.
*     IF  IX  IS LESS THAN 4, THEN WE DIVIDE IX BY 2 AND NOW IX MEASURES
*     THE NUMBER OF ELECTRONS WHICH HAVE BEEN CHANGED IN GOING FROM PSI
*     TO PSIP.  IF NOW IX=0, WE HAVE A DIAGONAL MATRIX ELEMENT.  RHO AND
*     SIG MAY TAKE ON ANY VALUES LESS THAN IHSH.  IF IX=1, ONE INTER-
*     ACTING SHELL ON EACH SIDE IS FIXED, WHILE THE OTHER MAY VARY.  IF
*     IX=2, ALL INTERACTING SHELLS ARE DETERMINED
*
      IF(IX.GT.4) RETURN
      CALL COUPLING(JA,JB)
      N1=2*IHSH-1
      IF((J1QN1(N1,2)-J1QN2(N1,2)).NE.0)RETURN
      IF((J1QN1(N1,3)-J1QN2(N1,3)).NE.0)RETURN
      IX=IX/2
      IF(IX-1.GT.0) THEN
*
* === UNIQUE SPECIFICATION OF INTERACTING SHELLS
*
        IF(ISIG.EQ.0) ISIG=IRHO
        IF(ISIGP.EQ.0) ISIGP = IRHOP
        IF(IBUG2.GT.0) WRITE(IWRITE,5) IRHO,ISIG,IRHOP,ISIGP
        IF(IGGG.EQ.40) THEN
          CALL NONRELAT2(IRHO,IRHOP)
        ELSEIF(IGGG.EQ.20) THEN
          CALL NONRELAT4(IRHO,ISIG,IRHOP,ISIGP)
        ELSE
          CALL NONRELAT5(IRHO,ISIG,IRHOP,ISIGP)
        ENDIF
      ELSEIF(IX-1.EQ.0) THEN
*
* === ONE INTERACTING SHELL SPECIFIED ON EACH SIDE. SUMMATION OVER OTHER
*
        IRSTO=IRHO
        IRPSTO=IRHOP
        DO 2 K1=1,IHSH
          IINE=1
          IIRE=1
          IF(NOSH1(K1).EQ.0) IIRE=0
          ISIG=K1
          IF(NOSH2(K1).EQ.0) IIRE=0
          ISIGP = K1
          IRHO=IRSTO
          IRHOP=IRPSTO
*
*     ORTHOGONALITY OF THE ORBITALS REQUIRES THAT THE VARYING INTER-
*     ACTING SHELL BE THE SAME ON BOTH SIDES OF THE MATRIX ELEMENT
*
* --- IRHO.LE.ISIG,   IRHOP.LE.ISIGP
*
          IF(IRHO.GT.ISIG) THEN
            ISTO=IRHO
            IRHO=ISIG
            ISIG = ISTO
          ELSEIF(IRHO.EQ.ISIG) THEN
            IF(NOSH1(ISIG).EQ.1) IIRE=0
            IF(NOSH1(ISIG).EQ.0) IINE=0
          ENDIF
          IF(IRHOP.GT.ISIGP) THEN
            ISTO=IRHOP
            IRHOP = ISIGP
            ISIGP = ISTO
          ELSEIF(IRHOP.EQ.ISIGP) THEN
            IF(NOSH2(ISIGP).EQ.1) IIRE=0
            IF(NOSH2(ISIGP).EQ.0) IINE=0
          ENDIF
          IF(IINE.EQ.1) THEN
            IF(IBUG2.GT.0) WRITE(IWRITE,5) IRHO,ISIG,IRHOP,ISIGP
            CALL NONRELAT3(IRHO,ISIG,IRHOP,ISIGP,IIRE)
          ENDIF
    2   CONTINUE
      ELSEIF(IX-1.LT.0) THEN
*
* === NO INTERACTING SHELLS SPECIFIED
*     SUMMATION OVER ALL POSSIBLE COMBINATIONS
*     IN THIS CASE, ORTHOGONALITY OF ORBITALS PRECLUDES ALL CASES
*     EXCEPT  IRHO=IRHOP    AND    ISIG=ISIGP
*
        DO 3 K1=1,IHSH
          IF(NOSH1(K1).NE.0) THEN
            ISIG=K1
            DO 4 K2=1,K1
              IIRE=1
              IF(NOSH1(K2).NE.0) THEN
                IRHO=K2
                IF(IRHO.EQ.ISIG) THEN
                  IF(NOSH1(ISIG).EQ.1) IIRE=0
                ENDIF
                IRHOP=IRHO
                ISIGP=ISIG
                IF(IBUG2.GT.0) WRITE(IWRITE,5) IRHO,ISIG,IRHOP,ISIGP
                CALL NONRELAT1(IRHO,ISIG,IIRE)
              ENDIF
    4       CONTINUE
          ENDIF
    3   CONTINUE
      ENDIF
      RETURN
      END
